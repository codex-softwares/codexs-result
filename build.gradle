plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.12.0'
}

description = 'Result monad for management of errors as values'
group = "ch.codexs.util"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.toVersion("11")
    targetCompatibility = JavaVersion.toVersion("11")
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'ch.codexs.util'
            artifactId = 'result'

            from components.java

            pom {
                name = 'result'
                description = 'Result monad for management of errors as values'
                url = 'https://github.com/codex-softwares/codexs-result'
                inceptionYear = '2024'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://spdx.org/licenses/MIT.html'
                    }
                }
                developers {
                    developer {
                        id = 'jocmer-codexs'
                        name = 'Jocelyn MÃ©rand'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/codex-softwares/codexs-result.git'
                    developerConnection = 'scm:git:ssh://git@github.com:codex-softwares/codexs-result.git'
                    url = 'https://github.com/codex-softwares/codexs-result'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                    authorization = 'BEARER'
                    applyMavenCentralRules = true
                    sourceJar = true
                    javadocJar = true
                }
            }
        }
    }
}